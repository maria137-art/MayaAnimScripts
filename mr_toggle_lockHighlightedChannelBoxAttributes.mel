
// ------------------------------------------------------------------------------------------------------------------------------------------------
// SCRIPT: mr_toggle_lockHighlightedChannelBoxAttributes.mel 
// VERSION: 0001
//
// CREATORS: Maria Robertson
// CREDIT: Brian Horgan / Michiel Duvekot
// -------------------------------------------------------------------
//
// DESCRIPTION: 
// A toggle for locking and unlocking highlighted attributes in the Channel Box.
// 
// ------------------------------------------------------------------- 
//
// RUN COMMAND:
// mr_toggle_lockHighlightedChannelBoxAttributes() ;
// 
// ------------------------------------------------------------------- 
// 
// RESEARCH THAT HELPED: 
// Code was based on what provided by Horganovski and mduvekoton CGSociety.
// https://forums.cgsociety.org/t/check-or-ignore-locked-coordinate-attributes/1470963
//
// ------------------------------------------------------------------------------------------------------------------------------------------------

global proc mr_toggle_lockHighlightedChannelBoxAttributes() {
    
    // initialize variables
    string $sel[] = `ls -sl` ;
    string $highlightedChannelBoxAttributes[] = `selectedChannelBoxAttributes` ;
    

    // for every selected item      
    for ($item in $sel) {
        
        // for each of its highlighted attributes
    	for ($attr in $highlightedChannelBoxAttributes) {
    	    
    	    string $individualAttr = $item + "." + $attr ;
    	    
    	    // if its locked, unlock it
    	    if (`getAttr -lock $individualAttr`) {
                setAttr -lock false $individualAttr ; 	        
    	    } 
    	    
    	    // otherwise lock it
    	    else {  
    		    setAttr -lock true $individualAttr ;
    		}
        }
    }
}

global proc mr_lockHighlightedChannelBoxAttributes() {
    
    // initialize variables
    string $sel[] = `ls -sl` ;
    string $highlightedChannelBoxAttributes[] = `selectedChannelBoxAttributes` ;
    
    // for every selected item      
    for ($item in $sel) {
        
        if (size($highlightedChannelBoxAttributes) == 0) {
            string $visible_attributes[] = `listAttr -keyable $item` ;

            for ($attr in $visible_attributes) {
                string $individualAttr = $item + "." + $attr ;
                // lock it
                setAttr -lock true $individualAttr ;
            }
            
        }
    
        else {
            // for each of its highlighted attributes
            for ($attr in $highlightedChannelBoxAttributes) {
                
                string $individualAttr = $item + "." + $attr ;
                // lock it
                setAttr -lock true $individualAttr ;
            }
        }
    }
}

global proc mr_unlockHighlightedChannelBoxAttributes() {
    
    // initialize variables
    string $sel[] = `ls -sl` ;
    string $highlightedChannelBoxAttributes[] = `selectedChannelBoxAttributes` ;
    
    // for every selected item      
    for ($item in $sel) {
        
        if (size($highlightedChannelBoxAttributes) == 0) {
            string $visible_attributes[] = `listAttr -keyable $item` ;

            for ($attr in $visible_attributes) {
                string $individualAttr = $item + "." + $attr ;
                // unlock it
                setAttr -lock false $individualAttr ;
            }
            
        }
    
        else {
            // for each of its highlighted attributes
            for ($attr in $highlightedChannelBoxAttributes) {
                
                string $individualAttr = $item + "." + $attr ;
                // unlock it
                setAttr -lock false $individualAttr ;
            }
        }
    }
}