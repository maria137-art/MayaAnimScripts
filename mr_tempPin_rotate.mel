
// ------------------------------------------------------------------------------------------------------------------------------------------------
// SCRIPT: tempPin_rotate
// VERSION: 0004
//
// CREATORS: Maria Robertson
// ------------------------------------------------------------------- 
//
// DESCRIPTION: Pins selected object/s to a single temp locator, to be only rotated in worldspace. When script is run again, constrained objects are keyed on current frame, and temp pin deleted.
// 
// ------------------------------------------------------------------- 
//
// RUN COMMAND:
// TempPinRotate() ;
//
// REQUIREMENTS:
// Must have mr_findConstraintTargets.mel in order to use the procedure FindConstraintTargets
// ------------------------------------------------------------------- 
// CHANGELOG:
// 0003: Added catch command, to stop script from failing if any translation or rotation attributes are locked.
// 0004: Keeping locator point constrained to objects, so that it follows them.
//                            
// ------------------------------------------------------------------------------------------------------------------------------------------------

global proc TempPinRotate() {
    
    // -------------------------------------------------------------------
    // 01. INITIALIZE VARIABLES
    // -------------------------------------------------------------------

    string $tempPinRotLocName = "TEMP_ROT_worldspace_locator" ;
    
    if (`objExists $tempPinRotLocName`) {
        select $tempPinRotLocName ;
        
        // use procedure
        FindConstraintTargets() ;
                     
        SetKeyTranslate ;
        SetKeyRotate ;
        
        delete $tempPinRotLocName ;      
    }
    
    else {     

        string $sel[] = `ls -sl` ;  
        
        // create the temp locator
        spaceLocator -p 0 0 0 -n $tempPinRotLocName ;
        setAttr ($tempPinRotLocName + "Shape.localScaleX") 10 ;
        setAttr ($tempPinRotLocName + "Shape.localScaleY") 10 ;
        setAttr ($tempPinRotLocName + "Shape.localScaleZ") 10 ;

         
        // rotate locator at the average orientation of selected objects   
        for ($item in $sel) { 
            catch (`orientConstraint $item $tempPinRotLocName`) ;
        }
        
        // delete the orient constrains 
        delete -cn $tempPinRotLocName ;
        
        // place locator at the average position of the selected objects   
        // then orient constrain the selected objects to the locator
        for ($item in $sel) { 
            catch (`pointConstraint $item $tempPinRotLocName`) ;
            catch (`orientConstraint -mo -weight 1 $tempPinRotLocName $item`) ;      
        }
        
        // lock translate of locator
        setAttr  -lock true ($tempPinRotLocName + ".translateX");
        setAttr  -lock true ($tempPinRotLocName + ".translateY");
        setAttr  -lock true ($tempPinRotLocName + ".translateZ");
  
        select $tempPinRotLocName ; 
    }       

}