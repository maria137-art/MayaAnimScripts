
// ------------------------------------------------------------------------------------------------------------------------------------------------ 
// SCRIPT: animLayerTools
// VERSION: 0001
//
// CREATORS: Maria Robertson
// CREDIT: Eric Pavey
// -------------------------------------------------------------------
//
// DESCRIPTION: 
// A bunch of mini-scripts to help work with animation layers faster.
// Took a while to research which commands to use, so gathered them here.
//
// This was made while learning MEL, so lots of commenting below.
//
// -------------------------------------------------------------------
//
// RESEARCH THAT HELPED:
// A great website by Eric Pavey for MEL info. Under the AnimLayer tag, there are 
// lots of snippets on anim layers: http://mayamel.tiddlyspot.com
// 
// ------------------------------------------------------------------------------------------------------------------------------------------------ 

// -------------------------------------------------------------------
// 00. SHOW ONLY ANIM CURVES OF BASE ANIMATION
// -------------------------------------------------------------------

global proc AnimLayer_ShowOnlyBaseAnimation() {
    
    // By default in the Graph Editor, selecting an object will display its curves on all
    // existing anim layers, even if locked or hidden.
    // This script can help focus on BaseAnimation, avoiding the clutter.
    
	// unhighlight all existing animation layers
    setSelectedForAllLayers(0) ;
    // highlight BaseAnimation
    animLayerEditorOnSelect "BaseAnimation" 1 ;
}


// ------------------------------------------------------------------------------------------------------------------------------------------------ 

// -------------------------------------------------------------------
// 00. SHOW ONLY ANIM CURVES OF HIGHLIGHTED ANIM LAYERS
// -------------------------------------------------------------------

global proc AnimLayer_ShowOnlyHighlightedAnimLayerCurves() {
      
    // Display only anim curves of highlighted animation layers for selected objects.
    
    // create array of highlighted anim layers
    string $highlightedAnimLayers[] = getSelectedAnimLayer("AnimLayerTab");
    
    // for every highlighted anim layer
    for ($item in $highlightedAnimLayers){
            // rehighlight it (to refresh the Graph Editor)
            // NOTE - 1 means highlight the layer, 0 means deselect it.
            animLayerEditorOnSelect($item, 1) ;
    }
} 

// the above procedure could be used with a scriptJob, so that this is automatically run whenever there is a change in selected objects
int $scriptJob_ShowOnlyHighlightedAnimLayerCurves = `scriptJob -e SelectionChanged AnimLayer_ShowOnlyHighlightedAnimLayerCurves` ;


// ------------------------------------------------------------------------------------------------------------------------------------------------ 

// -------------------------------------------------------------------
// 00. SELECT OBJECTS ON HIGHLIGHTED ANIM LAYERS
// -------------------------------------------------------------------

global proc AnimLayer_SelectHighlightedAnimLayerObjects_A() {
    // create array of highlighted anim layers
    string $highlightedAnimLayers[] = getSelectedAnimLayer("AnimLayerTab") ;
    // select any objects on them
    layerEditorSelectObjectAnimLayer($highlightedAnimLayers) ;
}


// ------------------------------------------------------------------------------------------------------------------------------------------------ 

// -------------------------------------------------------------------
// 00. SELECT HIGHLIGHTED ANIM LAYERS NODES
// -------------------------------------------------------------------

global proc AnimLayer_SelectAnimLayerNode() {

    // create array of highlighted anim layers
    string $highlightedAnimLayers[] = getSelectedAnimLayer("AnimLayerTab") ;
    // reselect them
    select $highlightedAnimLayers ;
    
}


// ------------------------------------------------------------------------------------------------------------------------------------------------ 

// -------------------------------------------------------------------
// 00. SELECT WEIGHT OF HIGHLIGHTED ANIM LAYERS
// -------------------------------------------------------------------

// Select the weights of selected animation layers
global proc AnimLayer_SelectWeightOfAnimLayers() {
    
    // create array of highlighted anim layers
    string $highlightedAnimLayers[] = getSelectedAnimLayer("AnimLayerTab") ;
    // clear selection, to help refresh Graph Editor
    select -cl  ;
    
    // for each anim layer
    for ($item in $highlightedAnimLayers) {
        if (`animLayer -q -selected $item`) {
            // add to selection
            select -add ($item + ".wgth") ;
        }
    }
}


// ------------------------------------------------------------------------------------------------------------------------------------------------ 

// -------------------------------------------------------------------
// 00. KEYFRAME WEIGHT OF HIGHLIGHTED ANIM LAYERS
// -------------------------------------------------------------------

global proc AnimLayer_KeyWeightNodes() {
    
    // create array of highlighted anim layers
    string $highlightedAnimLayers[] = getSelectedAnimLayer("AnimLayerTab");
    // clear selection, to help refresh Graph Editor
    select -cl  ;
        
    // key the weight nodes for selected animation layers
    for ($item in $highlightedAnimLayers){
        if (`animLayer -q -selected $item`){
            setKeyframe ($item + ".wgth") ;
        }
    }    
}


// ------------------------------------------------------------------------------------------------------------------------------------------------ 

// -------------------------------------------------------------------
// 00. GROUP HIGHLIGHTED ANIM LAYERS
// -------------------------------------------------------------------

// Create parent group for selected Animation Layers

global proc AnimLayer_CreateParentGroup() {
    
    // create array of highlighted anim layers
    string $highlightedAnimLayers[] = getSelectedAnimLayer("AnimLayerTab") ;
    
    // create anim layer called Group
    animLayer Group ;
    
    // for each highlighted anim layer
    for ($item in $highlightedAnimLayers){
        // parent them under Group
        animLayer -edit -parent Group $item ;
    }
}


// ------------------------------------------------------------------------------------------------------------------------------------------------ 

// -------------------------------------------------------------------
// 00. TOGGLE LOCK HIGHLIGHED ANIM LAYES
// -------------------------------------------------------------------

// Toggle locking selected animation layers

global proc AnimLayer_ToggleLock() {
    
    // create array of highlighted anim layers
    string $highlightedAnimLayers[] = getSelectedAnimLayer("AnimLayerTab") ;
    // for every layer
    for ($item in $highlightedAnimLayers){
        // declare its lock attribute
        string $LockedLayerAttribute = `getAttr ($item + ".lo")` ;
        
        // toggle off if on
        if ($LockedLayerAttribute == 1) {
            animLayer -edit -lock 0 $item ; 	        
        } 
        // toggle on if off
        else {  
    	    animLayer -edit -lock 1 $item ; 
    	}
    }
}


// ------------------------------------------------------------------------------------------------------------------------------------------------ 

// -------------------------------------------------------------------
// 00. FIND WHICH SCRIPT CONTAINS THE MAYA COMMAND SPECIFIED
// -------------------------------------------------------------------

// Find which script contains the Maya command specified.
// Helpful when trying to understand what it does.

whatIs layerEditorSelectObjectAnimLayer ;

